buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://jitpack.io' }
}


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.pluscubed.mishuzhushou"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 3
        versionName "1.1.0"
    }
    if (project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            //noinspection GroovyAssignabilityCheck
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS_MISHUZHUSHOU
                keyPassword RELEASE_KEY_PASSWORD_MISHUZHUSHOU
            }
        }
    }
    buildTypes {
        //noinspection GroovyMissingReturnStatement
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            ext.enableCrashlytics = false
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
    }
}

dependencies {
    compile 'com.github.afollestad.material-dialogs:core:0.8.5.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.makeramen:roundedimageview:2.2.1'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') {
        transitive = true;
    }
}
